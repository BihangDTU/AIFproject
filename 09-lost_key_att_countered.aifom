Problem: LOSS_KEY_ATT;

Types: 
TOKEN ={token1};

Sets: 
extract(TOKEN),		                
wrap(TOKEN),
unwrap(TOKEN),
decrypt(TOKEN),
sensitive(TOKEN);

Functions:
public senc/2, h/2,bind/3;
private inv/1; 

Facts:
iknows/1, attack/0;

Rules:
iik1(K1:value,N1:value)
=[K1,N1]=>N1 in sensitive(token1).N1 in extract(token1).iknows(h(N1,K1)).K1 in sensitive(token1);
iik2(K2:value,N2:value)
=[K2,N2]=>iknows(h(N2,K2)).N2 in wrap(token1).N2 in extract(token1);
iik3(K3:value,N3:value)
=[K3,N3]=>iknows(h(N3,K3)).N3 in extract(token1).N3 in decrypt(token1).K3 in decrypt(token1).iknows(K3);

% =====set wrap=====
setwrap(N2:value,K2:value)
N2 notin sensitive(token1).N2 notin decrypt(token1).iknows(h(N2,K2)) => N2 in wrap(token1);

% =====set unwrap===
setunwrap(N2:value,K2:value)
N2 notin sensitive(token1).iknows(h(N2,K2)) => N2 in unwrap(token1);

% =====unwrap, generate new handler======
%-----------add the wrap attr copy-------------
unwrapWrap(M2:untyped,K2:value,N1:value,N2:value,Nnew:value)
iknows(senc(M2,K2)).iknows(bind(N1,M2,K2)).N1 in wrap(token1).iknows(h(N2,K2)).N2 in unwrap(token1).=[Nnew]=> iknows(h(Nnew,M2)).Nnew in wrap(token1).N2 in unwrap(token1).N1 in wrap(token1);

%-----------add the senstive attr copy-------------
unwrapSens(M2:untyped,K2:value,N1:value,N2:value,Nnew:value)
iknows(senc(M2,K2)).iknows(bind(N1,M2,K2)).N1 in sensitive(token1).iknows(h(N2,K2)).N2 in unwrap(token1).=[Nnew]=> iknows(h(Nnew,M2)).Nnew in sensitive(token1).N2 in unwrap(token1).N1 in sensitive(token1);

%-----------add the decrypt attr copy-------------
decrypt1Attr(M2:untyped, K2:value,N1:value,N2:value,Nnew:value)
iknows(senc(M2,K2)).iknows(bind(N1,M2,K2)).N1 in decrypt(token1).iknows(h(N2,K2)).N2 in unwrap(token1).=[Nnew]=> iknows(h(Nnew,M2)).Nnew in decrypt(token1).N2 in unwrap(token1).N1 in decrypt(token1);

decrypt2Attr(M2:untyped, K2:value,N1:value,N2:value,Nnew:value)
iknows(senc(M2,K2)).iknows(bind(N1,M2,K2)).N1 notin wrap(token1).N1 notin sensitive(token1).N1 notin decrypt(token1).iknows(h(N2,K2)).N2 in unwrap(token1).=[Nnew]=> iknows(h(Nnew,M2)).N2 in unwrap(token1);

% ======================wrap================
wrap(N1:value,K1:value, N2:value, K2:value)
iknows(h(N1,K1)).N1 in extract(token1).N2 in wrap(token1).iknows(h(N2,K2)) 
=> iknows(senc(K1,K2)).N1 in extract(token1).N2 in wrap(token1).iknows(bind(N1,K1,K2));

% ======================bind generation================
bind1(K3:value,N2:value,K2:value, K1:value)
iknows(K3).iknows(h(N2,K2))=>iknows(bind(N2,K3,K3));
bind2(K3:value,N2:value,K2:value, K1:value)
iknows(K3).iknows(K1).iknows(h(N2,K2))=>iknows(bind(N2,K1,K3)).iknows(bind(N2,K3,K1));

% =====set decrypt===
setdecrypt(Nnew:value,K2:value)
Nnew notin wrap(token1).iknows(h(Nnew,K2)) => Nnew in decrypt(token1);

% ======================decrypt================
decrypt1(Nnew:value,K2:value,M1:untyped)
Nnew in decrypt(token1).iknows(h(Nnew,K2)).iknows(senc(M1,K2)) =>iknows(M1);
decrypt2(K2:value,M1:untyped)
iknows(senc(M1,K2)).iknows(K2)=>iknows(M1);

% ======================attacks================
attack(K1:value)
K1 in sensitive(token1).iknows(K1)=>attack;